
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьОписаниеМетеданных(Запрос, Конфигурация) Экспорт
	
	РезультатЗапросаПакет = Запрос.ВыполнитьПакет();
	
	СоответствиеКоллекцииМетаданных = Расш1_КоллекцияМетаданных.СоответствиеКоллекцииМетаданных();
	
	// Описание всех объектов конфигурации.
	КоллекцияМетаданных = Новый Структура();
	
	ЗаполнитьРеквизитыОбъектов(РезультатЗапросаПакет, СоответствиеКоллекцииМетаданных, КоллекцияМетаданных);
	ЗаполнитьПредопределенныеЗначения(РезультатЗапросаПакет, СоответствиеКоллекцииМетаданных, КоллекцияМетаданных);
	СохранитьОписаниеМетаданныхВИБ(КоллекцияМетаданных, Конфигурация);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыОбъектов(РезультатЗапросаПакет, СоответствиеКоллекцииМетаданных, КоллекцияМетаданных)
	
	ВыборкаВидМетаданных = РезультатЗапросаПакет[4].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВидМетаданных.Следующий() Цикл
		
		ОписаниеКоллекции = Новый Структура();
		
		ВидОбъекта = ВыборкаВидМетаданных.ВидОбъекта;
		
		ВыборкаРеквизитыИмяОбъект = ВыборкаВидМетаданных.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаРеквизитыИмяОбъект.Следующий() Цикл
			
			ИмяОбъекта = ВыборкаРеквизитыИмяОбъект.ИмяОбъекта;
			
			ОписаниеРеквизитов = Новый Структура();
			
			ВыборкаРеквизитыТЧ = ВыборкаРеквизитыИмяОбъект.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаРеквизитыТЧ.Следующий() Цикл
				
				Если ВыборкаРеквизитыТЧ.ТабличнаяЧасть.Пустая() Тогда
					
					// Это реквизиты объекта.
					ВыборкаРеквизиты = ВыборкаРеквизитыТЧ.Выбрать();
					Пока ВыборкаРеквизиты.Следующий() Цикл
						
						ИмяРеквизита = ВыборкаРеквизиты.Реквизит;
						СвойстваРеквизита = Новый Структура("name", ВыборкаРеквизиты.СинонимРеквизита);
						ОписаниеРеквизитов.Вставить(ИмяРеквизита, СвойстваРеквизита);
						
					КонецЦикла;
					
				Иначе
					
					// Это табличная часть.
					НаименованиеТЧ = ВыборкаРеквизитыТЧ.ТабличнаяЧасть.Наименование;
					ИмяТЧОбъекта = СтрЗаменить(НаименованиеТЧ, "{", "");
					ИмяТЧОбъекта = СтрЗаменить(ИмяТЧОбъекта, "}", "");
					ТабличнаяЧастьОбъекта = Новый Структура("name", ИмяТЧОбъекта);
					
					ВыборкаРеквизиты = ВыборкаРеквизитыТЧ.Выбрать();
					Пока ВыборкаРеквизиты.Следующий() Цикл
						
						ИмяРеквизита = ВыборкаРеквизиты.Реквизит;
						СвойстваРеквизита = Новый Структура("name", ВыборкаРеквизиты.СинонимРеквизита);
						ТабличнаяЧастьОбъекта.Вставить(ИмяРеквизита, СвойстваРеквизита);
						
					КонецЦикла;
					
					ОписаниеРеквизитов.Вставить(ИмяТЧОбъекта, ТабличнаяЧастьОбъекта);
					
				КонецЕсли;
				
			КонецЦикла;
			
			СтруктураОбъекта = Новый Структура("properties", ОписаниеРеквизитов);
			ОписаниеКоллекции.Вставить(ИмяОбъекта, СтруктураОбъекта);
			
		КонецЦикла;
		
		ВидОбъектаДляКоллекции = СоответствиеКоллекцииМетаданных[ВидОбъекта];
		КоллекцияМетаданных.Вставить(ВидОбъектаДляКоллекции, ОписаниеКоллекции);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПредопределенныеЗначения(РезультатЗапросаПакет, СоответствиеКоллекцииМетаданных, КоллекцияМетаданных)
	
	ВыборкаВидМетаданных = РезультатЗапросаПакет[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВидМетаданных.Следующий() Цикл
		
		ВидОбъекта = ВыборкаВидМетаданных.ВидОбъекта;
		ВидОбъектаДляКоллекции = СоответствиеКоллекцииМетаданных[ВидОбъекта];
		Если КоллекцияМетаданных.Свойство(ВидОбъектаДляКоллекции) Тогда
			ЭтоПеречисление = Ложь;
			ОписаниеКоллекции = КоллекцияМетаданных[ВидОбъектаДляКоллекции];
		Иначе
			ЭтоПеречисление = Истина;
			ОписаниеКоллекции = Новый Структура;
		КонецЕсли;
			
		ВыборкаИмяОбъекта = ВыборкаВидМетаданных.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИмяОбъекта.Следующий() Цикл
			
			ИмяОбъекта = ВыборкаИмяОбъекта.ИмяОбъекта;
			
			Если ОписаниеКоллекции.Свойство(ИмяОбъекта) Тогда
				СтруктураОбъекта = ОписаниеКоллекции[ИмяОбъекта];
			Иначе
				СтруктураОбъекта = Новый Структура;
			КонецЕсли;
			
			ОписаниеПредопределенных = Новый Структура();
			ВыборкаПредопределенноеЗначение = ВыборкаИмяОбъекта.Выбрать();
			Пока ВыборкаПредопределенноеЗначение.Следующий() Цикл
				
				Значение = ВыборкаПредопределенноеЗначение.ПредопределенныеЗначения;
				ОписаниеПредопределенных.Вставить(Значение, "");
				
			КонецЦикла;
			
			СтруктураОбъекта.Вставить("predefined", ОписаниеПредопределенных); 
			
			Если ЭтоПеречисление Тогда
				ОписаниеКоллекции.Вставить(ИмяОбъекта, СтруктураОбъекта);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЭтоПеречисление Тогда
			КоллекцияМетаданных.Вставить(ВидОбъектаДляКоллекции, ОписаниеКоллекции);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СохранитьОписаниеМетаданныхВИБ(КоллекцияМетаданных, Конфигурация)
	
	Файл = Новый ЗаписьJSON();
	Файл.УстановитьСтроку();
	Попытка
		ЗаписатьJSON(Файл, КоллекцияМетаданных);
	Исключение
		ВызватьИсключение("Не удалось сохранить коллекцию метаданных:" + Символы.ПС + ОписаниеОшибки());
	КонецПопытки;
	
	КоллекцияJSON = Файл.Закрыть();
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Конфигурация, КоллекцияJSON);
	
КонецПроцедуры

#КонецОбласти
