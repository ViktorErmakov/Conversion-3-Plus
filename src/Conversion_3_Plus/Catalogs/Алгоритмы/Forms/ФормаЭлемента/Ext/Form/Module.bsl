
#Область ОписаниеПеременных

&НаКлиенте
Перем КонсольКода;

&НаКлиенте
Перем СветлаяТема;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Расш1_КонсольКода.СоздатьКонсоль(Элементы.Алгоритм, ЭтотОбъект, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	ЭтотОбъект[Элементы.Алгоритм.Имя + "HTML"] = АдресКонсолиКода;
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	// Запишем новый текст в реквизит.
	Объект.Алгоритм = КонсольКода.getText();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АлгоритмHTMLСформирован(Элемент)
	
	КонсольКода = Элемент.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКода, Объект.Алгоритм);
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СменитьТему(Команда)
	
	СветлаяТема = Не СветлаяТема;
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, Элементы.Алгоритм.Имя + "HTML", СветлаяТема);
	Если СветлаяТема Тогда
		Элементы["СменитьТему"].Заголовок = "Темная";
	Иначе
		Элементы["СменитьТему"].Заголовок = "Светлая";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВхождения()
	КонсольКода.editor.trigger("", "editor.action.changeAll");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВыборКонвертации()
	
	Если СписокКонвертаций.Количество() > 1 Тогда
		Расш1_КонсольКода.ПоказатьВыборКонвертации(ЭтотОбъект, СписокКонвертаций);
	Иначе
		ВыборКонфигурацииКонсоли(0, Неопределено)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКонфигурацииКонсоли(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Конвертация = СписокКонвертаций[РезультатВопроса].Значение;
	ОписаниеМетаданныхJSON = ПолучитьОписаниеМетаданныхJSON(Конвертация);
	КонсольКода.updateMetadata(ОписаниеМетаданныхJSON);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОписаниеМетаданныхJSON(Конвертация)
	Возврат Расш1_КоллекцияМетаданных.ПолучитьОписаниеМетаданных(Конвертация);
КонецФункции

#КонецОбласти

#Если Клиент Тогда
	СветлаяТема = Истина;
#КонецЕсли
