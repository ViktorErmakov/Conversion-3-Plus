
#Область ОписаниеПеременных

&НаКлиенте
Перем КонсольКода;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Элементы.Алгоритм.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	HTMLДокумент = АдресКонсолиКода;
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	// Запишем новый текст в реквизит.
	Объект.Алгоритм = КонсольКода.getText();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Расш1_HTMLДокументДокументСформированПосле(Элемент)
	
	КонсольКода = Элементы.HTMLДокумент.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКода, Объект.Алгоритм);
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Расш1_СветлаяТемаПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "HTMLДокумент");
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ТемнаяТемаПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "HTMLДокумент", Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВыборКонвертации()
	
	Если СписокКонвертаций.Количество() > 1 Тогда
		Расш1_КонсольКода.ПоказатьВыборКонвертации(ЭтотОбъект, СписокКонвертаций);
	Иначе
		ВыборКонфигурацииКонсоли(0, Неопределено)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКонфигурацииКонсоли(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Конвертация = СписокКонвертаций[РезультатВопроса].Значение;
	ОписаниеМетаданныхJSON = ПолучитьОписаниеМетаданныхJSON(Конвертация);
	КонсольКода.updateMetadata(ОписаниеМетаданныхJSON);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОписаниеМетаданныхJSON(Конвертация)
	Возврат Расш1_КоллекцияМетаданных.ПолучитьОписаниеМетаданных(Конвертация);
КонецФункции

#КонецОбласти