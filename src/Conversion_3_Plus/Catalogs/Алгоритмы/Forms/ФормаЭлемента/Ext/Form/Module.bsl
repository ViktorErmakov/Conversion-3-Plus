#Область ОписаниеПеременных

&НаКлиенте
Перем КонсольКода;

&НаКлиенте
Перем СветлаяТема;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Расш1_КонсольКода.СоздатьКонсоль(Элементы.Алгоритм, ЭтотОбъект, ЭтотОбъект);
	
	Расш1_КонсольКода.СоздатьРеквизит(ЭтотОбъект, "Конвертация");
	
	// Создаем реквизит для хранения описания метаданных. Нужно хранить т.к. мы подаем это описание потом при формировании консолек.
	Расш1_КонсольКода.СоздатьРеквизит(ЭтотОбъект, "ОписаниеМетаданныхJSON");
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	ЭтотОбъект[Элементы.Алгоритм.Имя + "HTML"] = АдресКонсолиКода;
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	// Запишем новый текст в реквизит.
	Объект.Алгоритм = КонсольКода.getText();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АлгоритмHTMLСформирован(Элемент)
	
	КонсольКода = Элемент.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКода, Объект.Алгоритм);
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Расш1_КонсольКода.ПриНажатии(ЭтотОбъект, ДанныеСобытия);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СменитьТемуАлгоритмHTML(Команда)
	Расш1_КонсольКода.СменитьТему(ЭтотОбъект, Элементы.Алгоритм.Имя + "HTML", СветлаяТема);
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВхожденияАлгоритмHTML()
	КонсольКода.editor.trigger("", "editor.action.changeAll");
КонецПроцедуры

&НаКлиенте
Процедура ОтправкаАлгоритмHTML(Команда)
	ПриИзмененииНаправления(Команда);
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеАлгоритмHTML(Команда)
	ПриИзмененииНаправления(Команда);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВыборКонвертации()
	
	Если СписокКонвертаций.Количество() > 1 Тогда
		Расш1_КонсольКода.ПоискНаименованияТекущейКонвертации(ЭтотОбъект.ВладелецФормы, ЭтотОбъект);
	Иначе
		ЭтотОбъект["Конвертация"] = СписокКонвертаций[0].Значение;
	КонецЕсли;
	
	ОбновитьОписаниеМетаданных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОписаниеМетаданных()
	
	ЭтотОбъект["ОписаниеМетаданныхJSON"] = ПолучитьОписаниеМетаданныхJSON(ЭтотОбъект["Конвертация"]);
	ОбновитьОписаниеМетаданныхВоВсехКонсоляхОбработчиков();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОписаниеМетаданныхJSON(Знач Конвертация, Знач Направление = "")
	
	ОписаниеМетаданных = Расш1_КоллекцияМетаданных.ПолучитьОписаниеМетаданных(Справочники.Конвертации.НайтиПоНаименованию(Конвертация));
	
	// Заполнение общих параметров обработчиков ПОД.
	ПользовательскиеОбъекты = Новый Структура;
	
	Если Не ПустаяСтрока(Направление) Тогда
		КомпонентыОбмена = Справочники.Конфигурации.ПолучитьОписаниеКомпонентыОбмена(Направление);
		ПользовательскиеОбъекты.Вставить("КомпонентыОбмена", КомпонентыОбмена);
	КонецЕсли;
	ОписаниеМетаданных.Вставить("customObjects", ПользовательскиеОбъекты);
	
	ОписаниеМетаданныхJSON = Расш1_КоллекцияМетаданных.ПолучитьОписаниеМетаданныхJSON(ОписаниеМетаданных);
	Возврат ОписаниеМетаданныхJSON;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииНаправления(Команда)
	
	ОбновитьОписаниеМетаданныхПоНаправлению(Команда.Имя);
	ОбновитьОписаниеМетаданныхВоВсехКонсоляхОбработчиков();
	Элементы[Команда.Имя].Родитель.Заголовок = Элементы[Команда.Имя].Заголовок;
	Элементы[Команда.Имя].Родитель.Картинка = Элементы[Команда.Имя].Картинка;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОписаниеМетаданныхПоНаправлению(ИмяКоманды)
	
	Направление = ЭтотОбъект.Команды.Найти(ИмяКоманды).Заголовок;
	ЭтотОбъект["ОписаниеМетаданныхJSON"] = ПолучитьОписаниеМетаданныхJSON(ЭтотОбъект["Конвертация"], Направление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОписаниеМетаданныхВоВсехКонсоляхОбработчиков()
	КонсольКода.updateMetadata(ЭтотОбъект["ОписаниеМетаданныхJSON"]);
КонецПроцедуры

#КонецОбласти

#Если Клиент Тогда
	СветлаяТема = Истина;
#КонецЕсли
