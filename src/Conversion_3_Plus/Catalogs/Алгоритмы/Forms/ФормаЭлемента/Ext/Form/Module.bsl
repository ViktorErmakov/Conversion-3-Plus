#Область ОписаниеПеременных

&НаКлиенте
Перем КонсольКода;

&НаКлиенте
Перем СветлаяТема;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Расш1_КонсольКода.СоздатьКонсоль(Элементы.Алгоритм, ЭтотОбъект, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	ЭтотОбъект[Элементы.Алгоритм.Имя + "HTML"] = АдресКонсолиКода;
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	// Запишем новый текст в реквизит.
	Объект.Алгоритм = КонсольКода.getText();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АлгоритмHTMLСформирован(Элемент)
	
	КонсольКода = Элемент.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКода, Объект.Алгоритм);
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Расш1_КонсольКода.ПриНажатии(ЭтотОбъект, ДанныеСобытия);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СменитьТемуАлгоритмHTML(Команда)
	Расш1_КонсольКода.СменитьТему(ЭтотОбъект, Элементы.Алгоритм.Имя + "HTML", СветлаяТема);
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВхожденияАлгоритмHTML()
	КонсольКода.editor.trigger("", "editor.action.changeAll");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВыборКонвертации()
	
	Если СписокКонвертаций.Количество() > 1 Тогда
		Расш1_КонсольКода.ПоказатьВыборКонвертации(ЭтотОбъект, СписокКонвертаций);
	Иначе
		ВыборКонфигурацииКонсоли(0, Неопределено)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКонфигурацииКонсоли(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Конвертация = СписокКонвертаций[РезультатВопроса].Значение;
	ОписаниеМетаданныхJSON = ПолучитьОписаниеМетаданныхJSON(Конвертация);
	КонсольКода.updateMetadata(ОписаниеМетаданныхJSON);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОписаниеМетаданныхJSON(Знач Конвертация)
	
	ОписаниеМетаданных = Расш1_КоллекцияМетаданных.ПолучитьОписаниеМетаданных(Конвертация);
	//"customObjects":{
	//         "_СтруктураВыгрузки":{
	//            "ref": "classes.Структура",
	//            "properties":{
	//               "Номенклатура":{
	//                  "name":"Номенклатура",
	//                  "description":"Ссылка на справочник номенклатуры",
	//                  "ref":"catalogs.Товары"
	//               },
	//               "Остаток":{
	//                  "name":"Остаток"
	//               }
	//            },
	
	ОбъетыСобытия = Справочники.Конфигурации.ПолучитьОписаниеПараметровОбработчиков();
	ОписаниеМетаданных.Вставить("customObjects", ОбъетыСобытия);
	
	ОписаниеМетаданныхJSON = Расш1_КоллекцияМетаданных.ПолучитьОписаниеМетаданныхJSON(ОписаниеМетаданных);
	Возврат ОписаниеМетаданныхJSON;
	
КонецФункции

#КонецОбласти

#Если Клиент Тогда
	СветлаяТема = Истина;
#КонецЕсли
