#Область ОписаниеПеременных

&НаКлиенте
Перем КонсольКодаПриОтправке;

&НаКлиенте
Перем КонсольКодаПередПолучениемДанных;

&НаКлиенте
Перем КонсольКодаПриПолученниДанных;

&НаКлиенте
Перем ПриОтправкеДанныхHTMLСфомирован;

&НаКлиенте
Перем ПередПолучениемДанныхHTMLСформирован;

&НаКлиенте
Перем ПриПолученииДанныхHTMLСформирован;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Элементы.АлгоритмПриОтправкеДанных.Видимость = Ложь;
	Элементы.АлгоритмПередПолучениемДанных.Видимость = Ложь;
	Элементы.АлгоритмПриПолученииДанных.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	
	ПриОтправкеДанныхHTML = АдресКонсолиКода;
	
	Если Объект.ИспользоватьДляПолучения Тогда
		ПередПолучениемДанныхHTML = АдресКонсолиКода;
		ПриПолученииДанныхHTML = АдресКонсолиКода;
	Иначе
		ПередПолучениемДанныхHTMLСформирован = Истина;
		ПриПолученииДанныхHTMLСформирован = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	
	// Запишем новый текст в реквизиты.
	Объект.АлгоритмПриОтправкеДанных = КонсольКодаПриОтправке.getText();
	Объект.АлгоритмПередПолучениемДанных = КонсольКодаПередПолучениемДанных.getText();
	Объект.АлгоритмПриПолученииДанных = КонсольКодаПриПолученниДанных.getText();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Расш1_ПриОтправкеДанныхHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаПриОтправке = Элементы.ПриОтправкеДанныхHTML.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКодаПриОтправке, Объект.АлгоритмПриОтправкеДанных);
	ПриОтправкеДанныхHTMLСфомирован = Истина;
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередПолучениемДанныхHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаПередПолучениемДанных = Элементы.ПередПолучениемДанныхHTML.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКодаПередПолучениемДанных, Объект.АлгоритмПередПолучениемДанных);
	ПередПолучениемДанныхHTMLСформирован = Истина;
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриПолученииДанныхHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаПриПолученниДанных = Элементы.ПриПолученииДанныхHTML.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКодаПриПолученниДанных, Объект.АлгоритмПриПолученииДанных);
	ПриПолученииДанныхHTMLСформирован = Истина;
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Расш1_ПриОтправкеДанныхHTMLСветлаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПриОтправкеДанныхHTML");
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОтправкеДанныхHTMLТемнаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПриОтправкеДанныхHTML", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередПолучениемДанныхHTMLСветлаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПередПолучениемДанныхHTML");
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередПолучениемДанныхHTMLТемнаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПередПолучениемДанныхHTML", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриПолученииДанныхHTMLСветлаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПриПолученииДанныхHTML");
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриПолученииДанныхHTMLТемнаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПриПолученииДанныхHTML", Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВыборКонвертации()
	
	Если ПриОтправкеДанныхHTMLСфомирован 
		И ПередПолучениемДанныхHTMLСформирован
		И ПриПолученииДанныхHTMLСформирован Тогда
		
		Если СписокКонвертаций.Количество() > 1 Тогда
			Расш1_КонсольКода.ПоказатьВыборКонвертации(ЭтотОбъект, СписокКонвертаций);
		Иначе
			ВыборКонфигурацииКонсоли(0, Неопределено)
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКонфигурацииКонсоли(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Конвертация = СписокКонвертаций[РезультатВопроса].Значение;
	ОписаниеМетаданныхJSON = ПолучитьОписаниеМетаданныхJSON(Конвертация);
	КонсольКодаПриОтправке.updateMetadata(ОписаниеМетаданныхJSON);
	
	Если Объект.ИспользоватьДляПолучения Тогда
		КонсольКодаПередПолучениемДанных.updateMetadata(ОписаниеМетаданныхJSON);
		КонсольКодаПриПолученниДанных.updateMetadata(ОписаниеМетаданныхJSON);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОписаниеМетаданныхJSON(Знач Конвертация)
	Возврат Расш1_КоллекцияМетаданных.ПолучитьОписаниеМетаданных(Конвертация);
КонецФункции

#КонецОбласти

#Если Клиент Тогда
	ПриОтправкеДанныхHTMLСфомирован = Ложь;
	ПередПолучениемДанныхHTMLСформирован = Ложь;
	ПриПолученииДанныхHTMLСформирован = Ложь;
#КонецЕсли
