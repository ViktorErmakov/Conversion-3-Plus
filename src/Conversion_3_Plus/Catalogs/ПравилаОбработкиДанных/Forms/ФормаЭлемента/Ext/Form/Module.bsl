
#Область ОписаниеПеременных

&НаКлиенте
Перем КонсольКодаПриОбработке;

&НаКлиенте
Перем КонсольКодаВыборкаДанных;

&НаКлиенте
Перем ПриОбработкеHTMLСфомирован;

&НаКлиенте
Перем ВыборкаДанныхHTMLСформирован;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Элементы.АлгоритмПриОбработке.Видимость = Ложь;
	Элементы.АлгоритмВыборкаДанных.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	
	ПриОбработкеHTML = АдресКонсолиКода;
	
	Если Объект.ИспользоватьДляОтправки Тогда
		ВыборкаДанныхHTML = АдресКонсолиКода;
	Иначе
		ВыборкаДанныхHTMLСформирован = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	
	// Запишем новый текст в реквизиты.
	Объект.АлгоритмПриОбработке = КонсольКодаПриОбработке.getText();
	Объект.АлгоритмВыборкаДанных = КонсольКодаВыборкаДанных.getText();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Расш1_ПриОбработкеHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаПриОбработке = Элементы.ПриОбработкеHTML.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКодаПриОбработке, Объект.АлгоритмПриОбработке);
	ПриОбработкеHTMLСфомирован = Истина;
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ВыборкаДанныхHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаВыборкаДанных = Элементы.ВыборкаДанныхHTML.Документ.defaultView;
	Расш1_КонсольКода.ИнициализацияРедактора(КонсольКодаВыборкаДанных, Объект.АлгоритмВыборкаДанных);
	ВыборкаДанныхHTMLСформирован = Истина;
	ПоказатьВыборКонвертации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Расш1_ПриОбработкеHTMLСветлаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПриОбработкеHTML");
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОбработкеHTMLТемнаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ПриОбработкеHTML", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ВыборкаДанныхHTMLСветлаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ВыборкаДанныхHTML");
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ВыборкаДанныхHTMLТемнаяПосле(Команда)
	Расш1_КонсольКода.УстановитьТемуКонсоли(ЭтотОбъект, "ВыборкаДанныхHTML", Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВыборКонвертации()
	
	Если ПриОбработкеHTMLСфомирован И ВыборкаДанныхHTMLСформирован Тогда
		
		Если СписокКонвертаций.Количество() > 1 Тогда
			
			Оповещение = Новый ОписаниеОповещения("ВыборКонфигурацииКонсоли", ЭтотОбъект);
			ТекстВопроса = НСтр("ru = 'Какую конвертацию использовать в консоли кода?'");
			
			СписокКнопок = Новый СписокЗначений;
			Для Каждого Конвертация Из СписокКонвертаций Цикл
				Индекс = СписокКонвертаций.Индекс(Конвертация);
				СписокКнопок.Добавить(Индекс, Строка(Конвертация.Значение));
			КонецЦикла;
			
			ТекстЗаголовка = НСтр("ru = 'bsl консоль'");
			ПоказатьВопрос(Оповещение, ТекстВопроса, СписокКнопок, , Неопределено, ТекстЗаголовка);
			
		Иначе
			ВыборКонфигурацииКонсоли(0, Неопределено)
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКонфигурацииКонсоли(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Конвертация = СписокКонвертаций[РезультатВопроса].Значение;
	ОписаниеМетаданныхJSON = ПолучитьОписаниеМетаданныхJSON(Конвертация);
	КонсольКодаПриОбработке.updateMetadata(ОписаниеМетаданныхJSON);
	
	Если Объект.ИспользоватьДляОтправки Тогда
		КонсольКодаВыборкаДанных.updateMetadata(ОписаниеМетаданныхJSON);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОписаниеМетаданныхJSON(Конвертация)
	
	Конфигурация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Конвертация, "Конфигурация");
	Возврат ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Конфигурация);
	
КонецФункции

#КонецОбласти

#Если Клиент Тогда
	ПриОбработкеHTMLСфомирован = Ложь;
	ВыборкаДанныхHTMLСформирован = Ложь;
#КонецЕсли
