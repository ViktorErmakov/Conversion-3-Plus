#Область ПрограммныйИнтерфейс

// Возвращает описание коллекции метаданных конфигурации.
// 
// Возвращаемое значение:
//   - Соответствие.
//
Функция СоответствиеКоллекцииМетаданных() Экспорт
	
	КоллекцияМетаданных = Новый Соответствие;
	КоллекцияМетаданных.Вставить("Справочники", "catalogs");
	КоллекцияМетаданных.Вставить("Документы", "documents");
	КоллекцияМетаданных.Вставить("РегистрыСведений", "infoRegs");
	КоллекцияМетаданных.Вставить("РегистрыНакопления", "accumRegs");
	КоллекцияМетаданных.Вставить("РегистрыБухгалтерии", "accountRegs");
	КоллекцияМетаданных.Вставить("Обработки", "dataProc");
	КоллекцияМетаданных.Вставить("Отчеты", "reports");
	КоллекцияМетаданных.Вставить("Перечисления", "enums");
	КоллекцияМетаданных.Вставить("ОбщиеМодули", "commonModules");
	КоллекцияМетаданных.Вставить("ПланыСчетов", "сhartsOfAccounts");
	КоллекцияМетаданных.Вставить("БизнесПроцессы", "businessProcesses");
	КоллекцияМетаданных.Вставить("Задачи", "tasks");
	КоллекцияМетаданных.Вставить("ПланыОбмена", "exchangePlans");
	КоллекцияМетаданных.Вставить("ПланыВидовХарактеристик", "chartsOfCharacteristicTypes");
	КоллекцияМетаданных.Вставить("ПланыВидовРасчета", "chartsOfCalculationTypes");
	
	Возврат КоллекцияМетаданных;
	
КонецФункции

// Получает описание метаданных из регистра сведений БезопасноеХранилищеДанных.
//
// Параметры:
//  Конвертация	 - СправочникСсылка - Конвертации.
// 
// Возвращаемое значение:
//   - Структура.
//
Функция ПолучитьОписаниеМетаданных(Конвертация) Экспорт
	
	Если ТипЗнч(Конвертация) = Тип("СправочникСсылка.Конвертации") Тогда
		Релиз = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Конвертация, "Конфигурация");
	Иначе
		Релиз = Конвертация;
	КонецЕсли;
	Возврат ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Релиз);
	
КонецФункции

// Процедура - Обновить описание метаданных
//
// Параметры:
//  Релиз	 - 	 - 
//
Процедура ОбновитьОписаниеМетаданных(Релиз) Экспорт
	
	Запрос = Справочники.Объекты.ПолучитьОписаниеМетеданныхКонфигурации(Релиз);
	
	Справочники.Конфигурации.ЗаполнитьОписаниеМетеданных(Запрос, Релиз);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Преобразует структуру описания конфигурации в формат JSON.
//
// Параметры:
//  КоллекцияМетаданных	 - Структура - коллекция метаданных.
// 
// Возвращаемое значение:
//   - Строка - в формате JSON.
//
Функция ПолучитьОписаниеМетаданныхJSON(КоллекцияМетаданных) Экспорт
	
	Файл = Новый ЗаписьJSON();
	Файл.УстановитьСтроку();
	Попытка
		ЗаписатьJSON(Файл, КоллекцияМетаданных);
	Исключение
		Текст = НСтр("Не удалось сохранить коллекцию метаданных:" + Символы.ПС + ОписаниеОшибки());
		ВызватьИсключение Текст;
	КонецПопытки;
	
	Возврат Файл.Закрыть();
	
КонецФункции

#КонецОбласти
